
#foreign time :: fn(value: void*) -> s32;
timespec_t :: struct {
  tv_sec: s64,
  tv_nsec: s64,
}

#foreign clock_gettime :: fn(clock_id: s32, tp: timespec_t*) -> s32;

tm_t :: struct {
  tm_sec: s32,
  tm_min: s32,
  tm_hour: s32,
  tm_mday: s32,
  tm_mon: s32,
  tm_year: s32,
  tm_wday: s32,
  tm_yday: s32,
  tm_isdst: s32,
}

#foreign localtime_r :: fn(timep: s64*, result: tm_t*) -> tm_t*;

Timing :: struct;

impl Timing {
  // returns the timespec_t for the current time since epoch.
  get_timespec_since_epoch :: fn() -> timespec_t {
    ts: timespec_t;
    clock_gettime(0, &ts);
    return ts;
  }
  
  // returns a seconds value representing time since epoch
  seconds :: fn() -> f64 {
    ts := #self::get_timespec_since_epoch();
    return (ts.tv_sec as f64) + (ts.tv_nsec as f64) / (1_000_000_000.0 as f64);
  }

  // returns a milliseconds value representing time since epoch
  milliseconds :: fn() -> f64 {
    ts := #self::get_timespec_since_epoch();
    return (ts.tv_sec as f64) * 1_000.0 + (ts.tv_nsec as f64) / 1_000_000.0 as f64;
  }

  // returns a microseconds value representing time since epoch
  microseconds :: fn() -> f64 {
    ts := #self::get_timespec_since_epoch();
    return (ts.tv_sec as f64) * 1_000_000.0 + (ts.tv_nsec as f64) / 1_000.0;
  }

  // returns a minutes value representing time since epoch
  minutes :: fn() -> f64 {
    ts := #self::get_timespec_since_epoch();
    return (ts.tv_sec as f64) / 60.0 + (ts.tv_nsec as f64) / (60.0 * 1_000_000_000.0);
  }

  // returns an hours value representing time since epoch
  hours :: fn() -> f64 {
    ts := #self::get_timespec_since_epoch();
    return (ts.tv_sec as f64) / 3_600.0 + (ts.tv_nsec as f64) / ((3_600.0 * 1_000_000_000.0) as f64);
  }

  // returns a days value representing time since epoch
  days :: fn() -> f64 {
    ts := #self::get_timespec_since_epoch();
    return (ts.tv_sec as f64) / 86_400.0 + (ts.tv_nsec as f64) / ((86_400.0 * 1_000_000_000.0) as f64)
  }

  date_time :: fn() -> str {
    ts := #self::get_timespec_since_epoch();
    time_t: s64 = ts.tv_sec;
    tm: tm_t;
    localtime_r(&time_t, &tm);
    buffer: u8[64];
    snprintf(buffer, 64, "%04d-%02d-%02d %02d:%02d:%02d",
             tm.tm_year + 1900, tm.tm_mon + 1, tm.tm_mday,
             tm.tm_hour, tm.tm_min, tm.tm_sec);
    return str::from_ptr(buffer as u8*, buffer as u8* + strlen(buffer));
  }
}
