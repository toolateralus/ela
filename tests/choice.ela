import assertion::*;

choice OptionT!<T> {
  Some(T),
  None,
}

choice Math {
  Vector2 { x: f32, y: f32 },
  Vector3 { x: f32, y: f32, z: f32 },
}

#test fn test_init_tuple() {
  /* 
    this treats what would get normally parsed as a call,
    as some sort of a tuple literal.

    * we should be able to do this as well, i.e strong inference.
    * mut x : OptionT!<s32> = Some(10);
  */
  mut x := OptionT!<s32>::Some(10);
}

#test fn test_init_struct() {
  /* struct thing, kinda would just work with init list. */
  x := Math::Vector2.{
    x: 0.0,
    y: 0.0
  };
}

#test fn test_reassign_and_marker_style() {
  mut x := OptionT!<s32>::Some(10);
  x = OptionT!<s32>::None;
}

fn return_choice() -> Math {
  return Math::Vector2.{x: 0, y: 0};
}

fn get_value() -> Math {
  return Math::Vector2.{x: 10, y: 10};
}

#test fn test_pattern_matching_struct_patterns() {
  val := get_value();
  /* pattern match, named args. mutability optional */
  if val is Math::Vector2.{ x: mut x, y: mut y } {
    assert(x == 10 && y == 10, "pattern match destructure failed");
  } 

  if val is Math::Vector2.{ x: x, y: y } {
    assert(x == 10 && y == 10, "pattern match destructure failed 2");
  }

  /* again, positional. */
  if val is Math::Vector3.{ x: x, y: y, z: z } {
    assert(false, "pattern match failed");
  }
}

#test fn test_pattern_matching_tuple_patterns() {
  x := OptionT!<s32>::Some(10);
  /* pattern match, take tuple members as a destructured list */
  if x is OptionT!<s32>::Some(v) {
    // fmt::println(v);
  }
}

#test fn test_pattern_matching_in_switch() {
  const widget := Math::Vector2.{
    x: 0.0,
    y: 1.0,
  };

  mut hit := false;

  switch is widget {
    Math::Vector2.{x: x, y: y}: {
      hit = x == 0.0 && y == 1.0;
    }
    else: {
      assert(false, "hit nothing");
    }
  }
  assert(hit, "'switch is' failed");
}

fn choice_as_param(math: Math) {
  // TODO: do we want to allow  { !(math is Pattern)  } ?
  // TODO: or do we want        { math is not Pattern } ?
  if math is Math::Vector2.{x: _, y: _1} {
    // pass.
  } else {
    assert(false, "failed to pattern match parameter choice _type");
  }
}

#test fn test_choice_as_param() {
  choice_as_param(Math::Vector2.{x: 0, y: 0});
}

#test fn complex_types() {
  choice ArrayAndFunctionPtrTest {
    Struct {
      f: [f32; 3]
    },
    FnPtr {
      f: fn ()
    }
  }
  mut c: ArrayAndFunctionPtrTest = ArrayAndFunctionPtrTest::Struct.{f: .[4.0, 1.0, 2.0]}

  if c is ArrayAndFunctionPtrTest::Struct.{f: f} {
    std::c::printf("%f\n"c, f[0]);
  } else {
    assert(false, "failed to get array");
  }

  c = ArrayAndFunctionPtrTest::FnPtr.{f: fn() { std::c::printf("Hello, From a function pointer.\n"c); }};

  if c is ArrayAndFunctionPtrTest::FnPtr.{f: f} {
    f();
  } else {
    assert(false, "failed to get fn ptr");
  }
}