import rayui::*;
import rayui::{Slider, Timer, CheckButton};
import fs::*;

mut playing := false;
mut timer: Timer;
mut bpm : f32 = 120;
mut subdivisions: s32 = 1;
mut beats: s32 = 4;
mut total_notes: s32 = 4;

mut bg_color := LIGHTCYAN;

mut play_button_style := Style.{
  foreground: WHITE,
  background: CLEAR,
  highlighted: GREEN,
  font_size: 16,
  use_jiggly_text: true,
  jiggle_intensity: 5.0,
  jiggle_rate: 2.0,
};

const file_dropdown_style := Style.{
  foreground: WHITE,
  background: GRAY,
  highlighted: GREEN,
  font_size: 16,
  use_jiggly_text: true,
  jiggle_intensity: 5.0,
  jiggle_rate: 2.0,
}

const SequenceChannel_style := Style.{
  foreground: WHITE,
  background: GRAY,
  highlighted: GREEN,
  font_size: 16,
  use_jiggly_text: true,
  jiggle_intensity: 5.0,
  jiggle_rate: 2.0,
}

check_buttons: List!<CheckButton>;

const check_button_style := Style.{
  foreground: WHITE,
  background: RED,
  highlighted: GREEN,
  font_size: 16,
  use_jiggly_text: false,
  jiggle_intensity: 5.0,
  jiggle_rate: 2.0,
}

const reset_button_style := Style .{
  foreground: WHITE,
  background: RED,
  highlighted: GREEN,
  font_size: 16,
  use_jiggly_text: false,
  jiggle_intensity: 5.0,
  jiggle_rate: 2.0,
};

mut index := 0;
mut color_index := 0;

fn get_sequencer_style(mut style: Style) -> Style {
  color_index++;
  index := color_index % All_Colors.length;
  index_1 := index + 4 % All_Colors.length;
  style.foreground = All_Colors.data[index];
  style.background = All_Colors.data[index_1].with_alpha(75).scaled_rgb(0.5);
  style.highlighted = style.background.with_alpha(255);
  return style;
}

struct SequenceChannel {
  check_buttons: List!<CheckButton>,
  sound: Sound,
  active: bool,
  style: Style,
  volume : s32,
  old_volume : s32,
  volume_slider: Slider,
}

impl SequenceChannel {
  fn create(_sound: Sound, _style: Style, name: str) -> #self {
    mut self: #self;
    self.sound = _sound;
    self.style = _style;
    self.volume_slider = .{
      text: Some(String::from(name)),                     // text
      rectangle: .{x: 0, y: 0, width: 146, height: 36}, // rectangle
      style: _style,                   // style
      state: new(SliderState!<f32>.{
        min_value: 0,
        max_value: 100,
        value: &mut self.volume,
      }),
    };
    return self;
  }
  fn draw(*mut self, x: f32, y: f32) {
    if total_notes != self.check_buttons.length {
      while total_notes > self.check_buttons.length {
        _x : f32 = x + 150 + ((self.check_buttons.length as f32 * 40.0) as f32) + 2;
        self.check_buttons.push(.{
          value:      false,
          rectangle:  .{x: _x, y: y, width: 36, height: 36},
          style:      self.style,
          text:       None(),
          allow_hold: true,
        });
      }
      while self.check_buttons.length > total_notes {
        button := self.check_buttons.pop();
      }
    }
    
    self.volume_slider.rectangle.x = x + 2;
    self.volume_slider.rectangle.y = y + 2;
    self.volume_slider.draw();
    index := 0;
    for button in self.check_buttons.iter_mut() {
      button.rectangle.x = x + 150 + index * 40 + 2;
      button.rectangle.y = y + 2;
      index++;
      button.draw();
    }
  }
  fn update(*mut self, index: s32) {
    if self.volume != self.old_volume {
      self.old_volume = self.volume;
      SetSoundVolume(self.sound, (self.volume as f32) / 100.0);
    }
    if self.check_buttons[index].value {
      PlaySound(self.sound);
    }
  }
}

mut channels: List!<SequenceChannel>;

fn on_file_selected(option: String, _: *mut void);

mut file_dropdown: DropDown = .{
  rectangle: .{x: 0, y: 250, width: 125, height: 30},
  style: file_dropdown_style,
  on_option_selected: &mut on_file_selected,
  state: &mut DropDownState!<String>.{},
  text: Option!<String>::Some(String.{
    data: "New Channel"c as *mut u8,
    length: 11,
  }),
};

fn on_file_selected(option: String, _: *mut void) {
  channels.push(SequenceChannel::create(
    LoadSound(option.data),
    get_sequencer_style(check_button_style),
    option.as_str(),
  ));
}

fn sequencer() {
  if playing && timer.tick() {
    for channel in channels.iter_mut() {
      channel.update(index);
    }
    index = (index + 1 % total_notes) % total_notes;
  }
}

#static extensions: str[3] = .[
  ".wav",
  ".mp3",
  ".ogg",
];

fn main() {
  mut bpm_slider : Slider = .{
    text: Some(String::from("bpm")),
    rectangle: .{x: 0, y: 0, width: 150, height: 40},
    style: .{
      foreground: WHITE.with_alpha(75),
      background: BLACK,
      highlighted: GREEN,
      font_size: 16,
    },
    state: &mut SliderState!<f32>.{ 
      value: &mut bpm,
      min_value: 30,
      max_value: 240,
    },
  };

  mut subdivision_slider : Slider = .{
    text: Some(String::from("subdivisions")),
    rectangle: .{x: 0, y: 110, width: 150, height: 40},
    style: .{
      foreground: WHITE.with_alpha(75),
      background: BLACK,
      highlighted: GREEN,
      font_size: 16,
      use_jiggly_text: false,
      jiggle_intensity: 0.0,
      jiggle_rate: 0.0
    },
    state: new(SliderState!<f32>.{
      min_value: 1,
      max_value: 20
      value: &mut subdivisions
    }),
  };

  mut beats_slider : Slider = .{
    text: Some(String::from("beats")),
    rectangle: .{x: 150, y: 110, width: 150, height: 40},
    style: .{
      foreground: WHITE.with_alpha(75),
      background: BLACK,
      highlighted: GREEN,
      font_size: 16,
      use_jiggly_text: false,
      jiggle_intensity: 0.0,
      jiggle_rate: 0.0
    },
    state: &mut SliderState!<f32>.{
      value: &mut beats,
      min_value: 1,
      max_value: 20,
    },
  };

  bg_color.r -= 175;
  bg_color.g -= 175;
  bg_color.b -= 175;

  mut dir := Directory::open(".").unwrap();
  defer dir.deinit();

  InitAudioDevice();
  InitWindow(800, 600, "Sequencer"c);

  SetWindowState(ConfigFlags::FLAG_WINDOW_RESIZABLE);
  SetTargetFPS(1000);

  files := dir.files();
  file_dropdown.options = files.filter(fn(file: *const String) -> bool {
    for i in 0..3 {
      if file.as_str().ends_with(extensions[i]) {
        return true;
        fmt::printlnf("adding file: \033[1;3;34m%\033[0m", (file,));
      }
    }
    return false;
  });
  files.deinit();

  reset_label := String::from("reset (no undo)");
  remove_label := String::from("remove channel");
  play_label := String::from("play");
  while !WindowShouldClose() {
    ClearBackground(GRAY);
    BeginDrawing();
      if IsKeyPressed(KeyboardKey::KEY_SPACE) {
        playing = !playing;
        timer.last_tick_time = 0;
        index = 0;
      };

      mut seq_y := GetScreenHeight() / 2.0;
      mut seq_x := 0.0;

      for note in 0..total_notes {
        label_x := seq_x + 150.0 + (note as f32) * 40.0;
        beat := (note * beats) / total_notes + 1;
        subdiv := note % subdivisions + 1;
        mut text := format("%.%", (beat, subdiv), FormatOptions::default());
        DrawText(text.data, label_x as s32, (seq_y - 40.0) as s32, 12, WHITE);
        text.deinit();
      }

      if playing {
        cursor_x := seq_x + 150.0 + (index - 1 + total_notes) % total_notes * 40.0
        DrawRectangleLines(cursor_x as s32, seq_y, 40, (channels.length as s32) * 40, ORANGE);
      }

      sequencer();
      bpm_slider.draw();
      bpm = (bpm as s32) as f32;

      timer.interval = (1 / (bpm / 60)) / subdivisions;

      total_notes = beats * subdivisions;

      subdivision_slider.draw();
      beats_slider.draw();

      if button(reset_label, Rectangle.{x: (GetScreenWidth() as f32) - 210.0, y: 10, width: 200, height: 40}, reset_button_style) {
        while channels.length > 0 {
          channels.pop();
        }
      }
      if button(remove_label, Rectangle.{x: 0, y: 215, width: 125, height: 30}, reset_button_style) {
        if channels.length > 0 then channels.pop();
      }

      for channel in channels.iter_mut() {
        channel.draw(seq_x, seq_y);
        seq_y += 40;
      }

      file_dropdown.draw();

      {
        if playing {
          play_button_style.highlighted = RED;
        } else {
          play_button_style.highlighted = GREEN;
        }


        if button(play_label, Rectangle.{x: 0, y: 50, width: 100, height: 40}, play_button_style) {
          playing = !playing;
          timer.last_tick_time = 0;
          index = 0;
        }
      }
    EndDrawing();
  }
}
